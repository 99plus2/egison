;;;;;
;;;;;
;;;;; Order Library
;;;;;
;;;;;

;;
;; Types
;;
(type $Ordering {<Less> <Equal> <Greater>})

(define $ordering : (Matcher Ordering)
  (algebraic-data-matcher
    {<less> <equal> <greater>}))

(class $Ord $A :: Eq
  {[$lt? : (A A / Bool)]}
  {[$lte? : (A A / Bool) (lambda [$x $y] (or (lt? x y) (eq? x y)))]
   [$gte? : (A A / Bool) (lt? $2 $1)]
   [$gt? : (A A / Bool) (lte? $2 $1)]
   [$compare : (A A / Ordering)
    (lambda [$m $n]
      (if (lt? m n)
        <Less>
        (if (eq? m n)
          <Equal>
          <Greater>)))]})

;;
;; Functions
;;
(define $min : ({$A :: Ord} / A)
  (lambda [$ns]
    (foldl' (lambda [$x $y] (if (lt? x y) x y)) (car ns) (cdr ns))))

(define $max : ({$A :: Ord} / A)
  (lambda [$ns]
    (foldl' (lambda [$x $y] (if (gt? x y) x y)) (car ns) (cdr ns))))

(define $min-and-max : ({$A :: Ord} / A A)
  (lambda [$ns]
    (foldl' (lambda [$ret $x]
              (match ret [integer integer]
                {[[$min $max] (if (lt? x min) [x max]
                                (if (gt? x max) [min x]
                                  [min max]))]}))
           [(car ns) (car ns)]
           (cdr ns))))

(define $split-by-ordering : ({$A :: Ord} A / {A} {A} {A})
  (lambda [$xs $p]
    (match xs (list something)
      {[<nil> [{} {} {}]]
       [<cons $x $rs>
        (let {[[$ys1 $ys2 $ys3] (split-by-ordering rs p)]}
          (match (compare x p) ordering
            {[<less> [{x @ys1} ys2 ys3]]
             [<equal> [ys1 {x @ys2} ys3]]
             [<greater> [ys1 ys2 {x @ys3}]]}))]})))

(define $qsort : ({$A :: Ord} / {A})
  (match-lambda (list something)
    {[<nil> {}]
     [<cons $x <nil>> {x}]
     [$xs (let {[$n (length xs)]}
            (let {[$p (nth (quotient n 2) xs)]}
              (let {[[$ys1 $ys2 $ys3] (split-by-ordering xs p)]}
                {@(qsort ys1) @ys2 @(qsort ys3)})))]}))
