(define $b-tree
  (lambda [$a]
    (algebraic-data-matcher
      {<leaf a> <node (b-tree a) (b-tree a)>})))

(define $tree
  (lambda [$a]
    (matcher
      {[,$val []
        {[$tgt (match [val tgt] [(tree a) (tree a)]
                 {[[<lnode $x $ts> <lnode ,x ,ts>] {[]}]
                  [[_ _] {}]})]}]
       [<lnode $ $> [a (list (tree a))] ; Node whose children are seen as a list.
        {[<Node $x $ts> {[x ts]}]
         [_ {}]}]
       [<mnode $ $> [a (multiset (tree a))] ; Node whose children are seen as a multiset.
        {[<Node $x $ts> {[x ts]}]
         [_ {}]}]
       [<descendant $> [(tree a)]
        {[$t {t @(match-all t (tree a)
                   [<mnode _ <cons <descendant $x> _>> x])}]}]
       [$ [something]
        {[$tgt {tgt}]}]
       })))
