;;;
;;;
;;; Pattern-matching against sequence of natural numbers
;;;
;;;

; first 10 natural numbers
(test (take 10 nats))

; first 10 Pythagoras numbers
(test (take 10 (match-all nats (list integer)
                 [<join _ <cons $x
                   <join _ <cons $y
                    <join _ <cons (& ?(lambda [$z] (eq? (* z z) (+ (* x x) (* y y)))) $z)
                     _>>>>>>
                  [x y z]])))

; first 10 Pythagoras numbers
(test (take 10 (match-all nats (list integer)
                 [<join (& _
                           <snoc $z
                            <join _ <cons $x <join _ <cons (& ?(lambda [$y] (eq? (* y y) (- (* z z) (* x x)))) $y) _>>>>>)
                        _>
                  [x y z]])))

; first 10 prime numbers
(test (take 10 primes))

; first 10 twin prime pairs
(test (take 10 (match-all primes (list integer)
                 [<join _ <cons $n <cons ,(+ n 2) _>>>
                  [n (+ n 2)]])))

; first 5 prime triplets (pattern 1)
(test (take 5 (match-all primes (list integer)
                [<join _ <cons $n <cons ,(+ n 2) <cons ,(+ n 6) _>>>>
                 [n (+ n 2) (+ n 6)]])))

; first 5 prime triplets (pattern 2)
(test (take 5 (match-all primes (list integer)
                [<join _ <cons $n <cons ,(+ n 4) <cons ,(+ n 6) _>>>>
                 [n (+ n 2) (+ n 6)]])))

; first 5 prime deserts whose length is equal or greater than 10
(test (take 5 (match-all primes (list integer)
                [<join _ <cons $m
                  <cons (& ?(gte-i? $ (+ 10 m)) $n)
                   _>>>
                 [m n]])))